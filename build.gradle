plugins {
	id 'fabric-loom' version '1.8-SNAPSHOT'
	id 'maven-publish'
	id "org.jetbrains.kotlin.jvm" version "2.0.21"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

sourceSets.main.kotlin.srcDirs += 'src/main/java'

repositories { //woah be yee, Mavens apo thee
	maven { url 'https://jitpack.io' }
	maven {
		url "https://squiddev.cc/maven/"
	}
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.shedaniel.me/" }

	// Hex Casting Dependencies
	maven { url "https://maven.blamejared.com/" }
	maven {
		name "entity reach"
		url "https://maven.jamieswhiteshirt.com/libs-release/"
	}
	maven { url "https://mvn.devos.one/snapshots/" }
	maven {
		name = 'Ladysnake Mods'
		url = 'https://maven.ladysnake.org/releases'
	}
	maven {
		name = 'Kotlin for Forge'
		url = 'https://thedarkcolour.github.io/KotlinForForge/'
	}
	maven {
		url = "https://maven.theillusivec4.top/"
	}
	maven {
		name = 'GeckoLib'
		url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
	}
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
}

fabricApi {
	configureDataGeneration()
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"

	modImplementation "com.github.iPortalTeam.ImmersivePortalsMod:q_misc_util:$immptl-mc$minecraft_version"
	modImplementation "com.github.iPortalTeam.ImmersivePortalsMod:imm_ptl_core:$immptl-mc$minecraft_version"
	modImplementation "com.github.iPortalTeam.ImmersivePortalsMod:build:$immptl-mc$minecraft_version"

	modImplementation "at.petra-k.paucal:paucal-fabric-$minecraft_version:$paucal_version"
	modImplementation("at.petra-k.hexcasting:hexcasting-fabric-1.20.1:${hex_casting}"){
		exclude(group: "maven.modrinth:gravity-api")
	}
	modApi("me.shedaniel.cloth:cloth-config-fabric:$cloth_config")

	modImplementation files("${rootProject.rootDir}/libs/serialization-hooks-0.4.99999.jar")

	modImplementation "com.samsthenerd.inline:inline-common:$minecraft_version-$inline_version"

	modImplementation "vazkii.patchouli:Patchouli:$minecraft_version-$patchouli_version-FABRIC"

	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cardinal_components_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 17
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}